The [`Staking2`](../contracts/vulnerable/Staking2.sol) contract has 3 distinct
vulnerabilities.

## Logical

`reward` uses the _current_ token balance of the contract to compute the reward
owed to each user. What should happen instead is that each time a user unstakes,
a checkpoint is emitted that can be used to correctly compute the reward for
each user _at the moment of unstaking_. Because this isn't done, if 2 users
unstake consecutively without any other action being taken, the 2nd user will
receive too much reward. Part **C** of [`5.js`](../pocs/5.js) demonstrates this
vulnerability.

## Reentrancy

It isn't possible to reenter `unstake` because it has the `nonReentrant`
modifier. Reentering from `stake` => `unstake` is harmless. However, reentering
`unstake` => `stake` or `unstake` => `sendReward` with an `addReward` in between
is catastrophic because `tokenInfo[token].stakerInfo[staker].staked` is only
updated _after_ the transfer (`unstake` doesn't follow
checks-effects-interactions). Therefore, `staked` can end up being larger than
`token.balanceOf(address(this))`. When that happens, the staker will be paid
more than the whole reward for `token`. Part **B** of [`5.js`](../pocs/5.js) and
`attackTwo` of [`Staking2Attack`](../contracts/malicious/Staking2Attack.sol)
demonstrate this attack. This uses the `IERC777Recipient.tokensReceived`
callback for reentrancy, but you could use any kind of token that allows a user
callback when receiving tokens.

## Gas

This is the most subtle vulnerability in
[`Staking2`](../contracts/vulnerable/Staking2.sol)`. When a contract has some
code like:

```Solidity
(bool success, bytes memory returnData) = address(foo).call(calldata);
if (!success) {
    doSomething();
    return;
}
```

it can run into the all-but-one-64th rule established by
[EIP-150](https://eips.ethereum.org/EIPS/eip-150). There's a related [write-up
by `ronan.eth`](https://ronan.eth.link/blog/ethereum-gas-dangers/) that can also
help you understand the vulnerability. Essentially, if the call to `foo`
consumes at least 63 times as much gas as `doSomething()` needs, then it's
possible for the call to `foo` to revert due to out-of-gas (which can be forced
by the caller) and `doSomething()` still succeeds. If this causes unexpected
state changes in a contract, it's vulnerable to an "insufficient gas griefing"
attack.

You can see this vulnerability in
[`Staking2` here](../contracts/vulnerable/Staking2.sol#L115):
```Solidity
// non-reverting `safeTransfer`
(bool success, bytes memory returnData) = address(token).call(
    abi.encodeWithSelector(token.transfer.selector, _msgSender(), amount)
);
if (
    !success ||
    (
        returnData.length >= 32
            ? abi.decode(returnData, (bytes32)) != bytes32(uint256(1))
            : returnData.length > 0
    )
) {
    _tokenInfo.badlyBehaved = true;
    return;
}
```
Notice how this code _doesn't revert_ if the call fails, but instead sets a
flag in storage. This bug could also be triggered using an ERC777 callback, but
we thought it was more interesting to exploit it in the less obvious way.

Look for the use of raw `.call`, `.staticcall`, and `.delegatecall` to find this
vulnerability, **especially** if that call specifies an exact amount of gas to
be passed. In protocols that don't support metatransactions, this vulnerability
is _usually_ less severe. But if a protocol supports metatransactions in any way
(e.g. [ERC-2612](https://eips.ethereum.org/EIPS/eip-2612),
[ERC-4494](https://eips.ethereum.org/EIPS/eip-4494), or
[ERC-2771](https://eips.ethereum.org/EIPS/eip-2771)), it can be
catastrophic. This vulnerability doesn't exist if the code does
`require(success);` after the `.call`. You may also find this vulnerability if a
contract uses `create(` or `create2(` in inline assembly without requiring that
the return value is non-zero, but this usage is rarer. The popular `Address`
library from OpenZeppelin provides several wrappers named `function*Call*` which
avoid this kind of vulnerability by always doing `require(success);` after each
call.

It's worth noting another quirk of the EVM that complicates this attack:
`SSTORE` always fails if there's 2300 or less gas available. This is in spite of
the rules in [EIP-2200](https://eips.ethereum.org/EIPS/eip-2200),
[EIP-2929](https://eips.ethereum.org/EIPS/eip-2929), and
[EIP-3529](https://eips.ethereum.org/EIPS/eip-3529) which might make it cost
less. Therefore, the call that's vulnerable to insufficient gas griefing must
consume at least 144,900 gas for this attack to be viable. Of course the `LOG*`
opcodes aren't disabled when gas is below 2300, so that might also produce a
vulnerability in off-chain code during an insufficient gas griefing attack as
well.
